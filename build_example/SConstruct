import os

# CONSTANTS
STARPU_VERSION='1.3'
STARPU_LIB='starpu-' + STARPU_VERSION

# ERRORS
ERR_DEPENDENCY_NOT_FOUND=-1

def ensure_starpu(env, config):
    STARPU_PATH='STARPU_PATH'
    PKG_CONFIG_PATH='PKG_CONFIG_PATH'
    if not config.PkgConfigCheck(STARPU_LIB):
        if not os.getenv(STARPU_PATH):
            print(F'Try to set {STARPU_PATH} or set {PKG_CONFIG_PATH}')
            return False
        else:
            print(f'Adding {STARPU_PATH} to {PKG_CONFIG_PATH}...')
            p = os.path.join(os.getenv(STARPU_PATH), 'lib', 'pkgconfig')
            env.AppendENVPath(PKG_CONFIG_PATH, p)
            return config.PkgConfigCheck(STARPU_LIB)
    return True

def config_compilers(env):
    print('updating flags')
    env.ParseConfig(f'pkg-config --cflags --libs {STARPU_LIB}')

env = Environment() #  start with default environment
env.Tool('scons-pkg-config', toolpath=['external']) # add pkg-config tool
config = env.Configure()

if not ensure_starpu(env, config):
    print(f'Cannot continue')
    exit(ERR_DEPENDENCY_NOT_FOUND)
    
config_compilers(env)

# ask other scripts what shoould be build
# duplicate=0 to avoid copy of the src files
SConscript('libumpalumpa/SConscript', variant_dir='build', duplicate=0, exports='env')

